plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

loom {
    forge {
        // mixinConfig "modid.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven { url = "https://maven.lukegrahamlandry.ca" }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    implementation project(path: ":Forge", configuration: "namedElements")
    common(project(path: ":Common", configuration: "namedElements")) { transitive false }

    // transformProductionForge is after is does the @ExpectPlatform injections
    // does not exist on :Forge of course because that's not the direction it goes in
    // namedElements on :Common would throw assertion errors on the platform classes
    shadowCommon project(path: ":Common", configuration: "transformProductionForge")
    shadowCommon project(path: ":Forge", configuration: "namedElements")


    modImplementation ("net.tslat.smartbrainlib:SmartBrainLib-Forge:1.4+1.19") {transitive=false}
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
    inputs.property "META-INF/mods.toml", project.version
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"  // not obfuscated, with shadowed stuff

    exclude 'ca/lukegrahamlandry/lib/mod/forge/**'
    relocate 'ca.lukegrahamlandry.lib', 'ca.lukegrahamlandry.examplemod.shadow.wrapperlib'

}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null  // obfuscated, with shadowed stuff
}

task renameJarForPublication(type: Zip, dependsOn: remapJar) {
    from remapJar.archiveFile.map { zipTree(it) }
    extension "jar"
    metadataCharset "UTF-8"
    destinationDirectory = base.libsDirectory
    classifier project.name
}

assemble.dependsOn renameJarForPublication

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
